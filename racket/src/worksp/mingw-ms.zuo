#lang zuo

(provide make-mingw-to-ms-link-libraries)

(define (make-mingw-to-ms-link-libraries m dir config)
  (cond
    [(member m '("ta6nt" "ti3nt"))
     ;; Synthesize a library that links `sin`, `cos` and `pow` to msvcrt,
     ;; instead of using MinGW's implementation
     (define dlltool (find-cc-like-tool config 'DLLTOOL "dlltool"))

     (define msfuns-def (build-path dir "msfuns.def"))
     (define out (fd-open-output msfuns-def :truncate))
     (fd-write out (~a "LIBRARY msvcrt\n"
                       "EXPORTS\n"
                       "  sin\n"
                       "  cos\n"
                       "  pow\n"))
     (fd-close out)

     (define msfuns-a (path-replace-extension msfuns-def ".a"))

     (define proc (hash-ref
                   (shell dlltool "-d" msfuns-def "-l" msfuns-a)
                   'process))

     (process-wait proc)
     (or (and (equal? 0 (process-status proc))
              (list msfuns-a))
         '())]
    [else '()]))

(define (find-cc-like-tool config key name)
  (or (hash-ref config key #f)
      (let ([cc (hash-ref config 'CC #f)])
        (cond
          [(and cc (glob-match? "*gcc" cc))
           (~a (substring cc 0 (- (string-length cc) 3)) name)]
          [(and cc (glob-match? "*cc" cc))
           (~a (substring cc 0 (- (string-length cc) 2)) name)]
          [else
           name]))))
